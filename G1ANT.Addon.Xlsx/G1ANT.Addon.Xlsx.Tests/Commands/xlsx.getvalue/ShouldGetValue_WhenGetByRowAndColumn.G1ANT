addon xlsx version 2.205.17304.1000
addon core version 4.99.18340.1354
addon language version 4.99.18340.1354

♥folderPath = ♥environment⟦TEMP⟧\
♥filePath = ♥folderPath + "TempTestDebug.xlsx"
♥testValue1 = ‴ABC‴
♥testValue2 = ‴123‴
♥testValue3 = ‴123%‴
♥testValue4 = ‴0.123‴

procedure DeleteTestFileIfExists
    ⊂
        if(System.IO.File.Exists(♥filePath))
        {
            System.IO.File.Delete(♥filePath);
        }
    ⊃
end procedure

procedure CreateWhenOpening
    xlsx.open path ♥filePath createifnotexist true
    xlsx.close
end procedure

procedure SetValuesInTestFile
    try errorcall testFailed
    xlsx.open path ♥filePath accessmode readwrite   
    xlsx.setvalue value ♥testValue1 row 1 colindex 4 result ♥res1
    xlsx.setvalue value ♥testValue2 row 2 colindex 3 result ♥res2
    xlsx.setvalue value ♥testValue3 row 3 colindex 2 result ♥res3
    xlsx.setvalue value ♥testValue4 row 4 colindex 1 result ♥res4
    xlsx.close
    test condition ⊂♥res1 != false && ♥res2 != false && ♥res3 != false && ♥res4 != false⊃
    end try
end procedure

procedure GetValuesFromTestFile
    xlsx.open path ♥filePath accessmode readwrite
    xlsx.getvalue row 1 colindex 4 result ♥res1
    test condition ⊂♥res1 == ♥testValue1⊃
    xlsx.getvalue row 2 colindex 3 result ♥res2
    test condition ⊂♥res2 == ♥testValue2⊃
    xlsx.getvalue row 3 colindex 2 result ♥res3
    test condition ⊂♥res3 == ♥testValue3⊃
    xlsx.getvalue row 4 colindex 1 result ♥res4
    test condition ⊂♥res4 == ♥testValue4⊃
    xlsx.close
end procedure

call DeleteTestFileIfExists
call CreateWhenOpening
call SetValuesInTestFile
call GetValuesFromTestFile
call DeleteTestFileIfExists
