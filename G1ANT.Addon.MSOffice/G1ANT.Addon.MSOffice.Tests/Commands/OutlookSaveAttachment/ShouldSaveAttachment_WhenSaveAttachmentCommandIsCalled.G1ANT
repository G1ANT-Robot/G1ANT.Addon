addon language version 4.98.18332.1321
addon msoffice version 2.205.17304.1000
addon core version 4.98.18332.1321

♥outlookInboxFolder = ‴t.g1ant.test@gmail.com\Skrzynka odbiorcza‴
♥toBeSentAttachmentPath = ‴♥environment⟦TEMP⟧\testattachmentsent.txt‴
♥toBeDownloadedAttachmentPath = ‴♥environment⟦TEMP⟧\testattachmentdownloaded.txt‴
♥testAttachmentText = ‴Test text.‴

file.delete ♥toBeSentAttachmentPath timeout 2
file.delete ♥toBeDownloadedAttachmentPath timeout 2

outlook.open display false

text.write text ♥testAttachmentText filename ♥toBeSentAttachmentPath

♥attachmentsToBeSent = ⟦list⟧♥toBeSentAttachmentPath❚ 

outlook.newmessage to ‴t.g1ant.test@gmail.com‴ subject ‴Test_mail_♥date‴ body ‴Test content.‴ attachments ♥attachmentsToBeSent
outlook.send 
delay 3

outlook.open display false

for ♥a from 0 to 10
    delay 1
    call CheckIfTestMessageHasCome  
    break if ♥testMailCome
    if ♥a>9
        call TestFailed
    end
end

outlook.getfolder path ♥outlookInboxFolder result ♥InboxFolder
♥emails = ♥InboxFolder⟦mails⟧
♥mail = ♥emails⟦♥emails.Count⟧
♥attachments = ♥mail⟦attachments⟧
♥attachment = ♥attachments⟦1⟧
outlook.saveattachment ♥attachment path ♥toBeDownloadedAttachmentPath
text.read filename ♥toBeDownloadedAttachmentPath result ♥textFromDownloadedAttachemnt
outlook.close

file.delete ♥toBeSentAttachmentPath
file.delete ♥toBeDownloadedAttachmentPath
test ♥textFromDownloadedAttachemnt==♥testAttachmentText

procedure CheckIfTestMessageHasCome
    outlook.findmails search ‴Test_mail_♥date‴ result ♥testMailCome
    ♥testMailCome = ⟦bool⟧♥testMailCome 
end

procedure TestFailed
    outlook.close
    file.delete ♥toBeSentAttachmentPath
    file.delete ♥toBeDownloadedAttachmentPath
    test false
end